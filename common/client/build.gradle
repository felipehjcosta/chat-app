apply plugin: "kotlin-multiplatform"
apply plugin: 'kotlinx-serialization'

kotlin {
    jvm {
        compilations.main.kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
        }
    }
    js {
        compilations.main.kotlinOptions {
            moduleKind = "commonjs"
            suppressWarnings = true
            freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
        }
        compilations.test.kotlinOptions {
            moduleKind = "commonjs"
            suppressWarnings = true
            freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                api project(":common:core")
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
            }
        }

        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }

        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
                implementation 'com.squareup.okhttp3:okhttp:3.11.0'
            }
        }

        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
                implementation "junit:junit:4.12"
                implementation "io.mockk:mockk:1.8.13.kotlin13"
                implementation 'com.squareup.okhttp3:mockwebserver:3.11.0'
            }
        }

        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
            }
        }

        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
                implementation "org.jetbrains:kotlin-mocha:$kotlin_mocha_version"
            }
        }
    }

    // Configure all compilations of all targets:
    targets.all {
        compilations.all {
            kotlinOptions {
                allWarningsAsErrors = true
            }
        }
    }
}

apply from: file("$rootProject.projectDir/gradleScripts/node_js.gradle")
apply from: file("$rootProject.projectDir/gradleScripts/test_mocha_js.gradle")
