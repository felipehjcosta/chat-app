apply plugin: 'kotlin2js'
apply plugin: 'com.moowork.node'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

compileKotlin2Js.kotlinOptions {
    moduleKind = "commonjs"
    outputFile = "${projectDir}/node/index.js"

//    sourceMap = true
//    sourceMapEmbedSources = "always"
}

node {
    version = '9.3.0'
    download = true
}

task installEspress(type: NpmTask, dependsOn: 'npmInstall') {
    args = ['install', 'express@4.16.2', '--save']
}

task installKotlin(type: NpmTask, dependsOn: 'npmInstall') {
    args = ['install', 'kotlin@1.2.20', '--save']
}

task installSocketIO(type: NpmTask, dependsOn: 'npmInstall') {
    args = ['install', 'socket.io@2.0.4', '--save']
}

task runNode(type: NodeTask) {
    script = file('node')
}

task run(type: GradleBuild) {
    tasks = ['installEspress', 'installKotlin', 'installSocketIO', 'build', 'runNode']
}

task assembleNode(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/node"

    dependsOn classes
}

assemble.dependsOn assembleNode
