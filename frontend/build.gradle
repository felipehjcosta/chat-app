group 'com.github.felipehjcosta'
version '1.0-SNAPSHOT'

apply plugin: "org.jetbrains.kotlin.js"
apply plugin: 'kotlin-dce-js'

repositories {
    maven { url "https://kotlin.bintray.com/kotlin-js-wrappers/" }
}

kotlin {
    target {
        browser {
            configure([compilations.main, compilations.test]) {
                kotlinOptions {
                    metaInfo = true
                    sourceMap = true
                    moduleKind = 'commonjs'
                    main = "call"
                    allWarningsAsErrors = true
                }
            }
            webpackTask {
                runTask {
                    //todo: use dsl after KT-32016 will be fixed
                    devServer = new org.jetbrains.kotlin.gradle.targets.js.webpack.KotlinWebpackConfig.DevServer(
                            true, false, true, true, false,
                            8081,
                            ["/api": "http://0.0.0.0:8082"],
                            ["$projectDir/src/main/resources".toString()]
                    )
                    outputFileName = "chatapp-frontend.js"
                }
            }
            testTask {
                useKarma {
                    usePhantomJS()
                }
            }
        }
    }
    sourceSets {
        main {
            kotlin.srcDirs += 'src/main/kotlin'
            dependencies {
                implementation project(":common:client")
                implementation kotlin("stdlib-js")

                implementation "org.jetbrains:kotlin-react:16.9.0-pre.89-kotlin-1.3.60"
                implementation "org.jetbrains:kotlin-react-dom:16.9.0-pre.89-kotlin-1.3.60"
                implementation "org.jetbrains:kotlin-react-router-dom:4.3.1-pre.89-kotlin-1.3.60"
                implementation "org.jetbrains:kotlin-extensions:1.0.1-pre.89-kotlin-1.3.60"

                implementation npm("kotlinx-html", "0.6.12")
                implementation npm("@jetbrains/kotlin-react", "16.9.0-pre.89")
                implementation npm("@jetbrains/kotlin-react-dom", "16.9.0-pre.89")
                implementation npm("@jetbrains/kotlin-react-router-dom", "4.3.1-pre.89")
                implementation npm("@jetbrains/kotlin-extensions", "1.0.1-pre.89")
                implementation npm("react", "16.9.0")
                implementation npm("react-dom", "16.9.0")
                implementation npm("react-router-dom", "4.3.1")
            }
        }
        test {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
            }
        }
    }
}
